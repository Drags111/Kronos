unit kronos.lib.wrappers.Renderable;

(*
Renderable
==========

The Renderable class is the highest level in the heiarchy of objects and
characters. It contains the basic functions of those, such as tile position and
plane. The map goes something like this:
                                                  -> Player
                                   -> Character -|
  Renderable -> RenderableEntity -|               -> NPC
                                   -> Object

*)

interface

  uses
    kronos.smart.SMART,
    kronos.lib.core.Hooks,
    kronos.lib.core.Globals;

  type
    TRenderable = class abstract
      public
        Obj: Integer;

        constructor Create(Obj: Integer);
        destructor Destroy; override;

        function GetLocalX: Integer;
        function GetLocalY: Integer;
        function GetPos: TTile;
        function GetPlane: Integer;

        function GetOrientation: Integer; virtual; abstract;
    end;

implementation

(*
Create
~~~~~~

.. code-block:: pascal

    constructor TRenderable.Create(Obj: Integer);

Creates the TRenderable object and sets the private field Obj to the one
specified in the parameter.

.. note::

  by Drags111

*)
constructor TRenderable.Create(Obj: Integer);
begin
  inherited Create;
  Self.Obj := Obj;
  UnfreedObjects.Add(Self);
end;

(*
Destroy
~~~~~~~

.. code-block:: pascal

    destructor TRenderable.Destroy; override;

Overrides the Destroy method of the TRenderable class. Called when
TRenderable.Free is declared, this procedure will free the object reference Obj
from SMART's memory.

.. note::

  by Drags111

*)
destructor TRenderable.Destroy; override;
begin
  if not SmartIsNull(Obj)then
    SmartFreeObject(Obj);
  UnfreedObjects.Remove(Self);
  inherited;
end;

(*
GetPlane
~~~~~~~~

.. code-block:: pascal

    function TRenderable.GetPlane: Integer;

Returns the current plane of the Renderable object. (above ground, underground,
etc).

.. note::

  by Drags111

*)
function TRenderable.GetPlane: Integer;
begin
  Result := SmartGetFieldByte(Obj, hook_animable_Plane);
end;

(*
GetLocalX
~~~~~~~~~

.. code-block:: pascal

    function TRenderable.GetLocalX: Integer;

Returns the current local X position of the Renderable object.

.. note::

  by Drags111

*)
function TRenderable.GetLocalX: Integer;
begin
  Result := SmartGetFieldInt(Obj, hook_animable_PixelX);
end;

(*
GetLocalY
~~~~~~~~~

.. code-block:: pascal

    function TRenderable.GetLocalY: Integer;

Returns the current local Y position of the Renderable object.

.. note::

  by Drags111

*)
function TRenderable.GetLocalY: Integer;
begin
  Result := SmartGetFieldInt(Obj, hook_animable_PixelY);
end;

(*
GetPos
~~~~~~

.. code-block:: pascal

    function TRenderable.GetPos: TTile;

Returns the current tile of the Renderable object.

.. note::

  by Drags111

*)
function TRenderable.GetPos: TTile;
begin
  Result.X := SmartGetFieldInt(0, hook_static_BaseX) + (GetLocalX shr 9);
  Result.Y := SmartGetFieldInt(0, hook_static_BaseY) + (GetLocalY shr 9);
end;

end.